(ns kami.scene.fixture-sync
  "Sync/check legacy kami-engine scene EDN fixtures from the authority repo."
  (:require [clojure.java.io :as io]
            [kami.scene.contracts :as contracts]
            [kami.scene.mirror-check :as mirror]))

(def default-engine-root mirror/default-engine-root)

(defn fixture-target [engine-root entry]
  (io/file (mirror/engine-path engine-root entry)))

(defn sync-plan
  "Return planned fixture writes with authority text and current target status."
  ([] (sync-plan default-engine-root))
  ([engine-root]
   (mapv (fn [entry]
           (let [target (fixture-target engine-root entry)
                 authority (contracts/contract-text entry)
                 current (when (.exists target) (slurp target))]
             {:entry entry
              :path (.getPath target)
              :status (cond
                        (and (nil? current)
                             (mirror/retired-engine-fixture? engine-root entry))
                        :retired

                        (nil? current) :missing
                        (= authority current) :same
                        :else :different)
              :text authority}))
         (:contracts (contracts/catalog)))))

(defn check-plan [plan]
  {:ok? (every? #{:same :retired} (map :status plan))
   :checked (count plan)
   :stale (vec (remove #(#{:same :retired} (:status %)) plan))})

(defn write-plan! [plan]
  (doseq [{:keys [path text]} plan]
    (io/make-parents path)
    (spit path text))
  {:written (count plan)})

(defn usage []
  (str "Usage:\n"
       "  clojure -M:check-engine-fixtures [../kami-engine]\n"
       "  clojure -M:sync-engine-fixtures [../kami-engine]\n"))

(defn -main [& args]
  (let [mode (first args)
        engine-root (or (second args) default-engine-root)
        plan (sync-plan engine-root)]
    (case mode
      "--check"
      (let [result (check-plan plan)]
        (if (:ok? result)
          (println "scene-fixtures-ok" (:checked result) "files")
          (do
            (binding [*out* *err*]
              (println "scene fixtures stale against" engine-root)
              (doseq [{:keys [entry path status]} (:stale result)]
                (println (name status) (pr-str entry) path)))
            (System/exit 1))))

      "--write"
      (let [result (write-plan! plan)]
        (println "scene-fixtures-written" (:written result) "files"))

      (do
        (binding [*out* *err*] (print (usage)))
        (System/exit 2)))))
