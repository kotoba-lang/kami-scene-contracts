(ns kami.scene.mirror-check
  "Check that legacy kami-engine scene EDN mirrors this authority repo."
  (:require [clojure.edn :as edn]
            [clojure.java.io :as io]
            [clojure.string :as str]
            [kami.scene.contracts :as contracts]))

(def default-engine-root "../kami-engine")

(defn engine-path
  [engine-root {:keys [domain file]}]
  (str engine-root "/" (name domain) "/data/" file))

(defn retired-engine-fixture?
  [engine-root {:keys [domain]}]
  (not (.exists (io/file engine-root (name domain) "data"))))

(defn read-file-edn [path]
  (edn/read-string (slurp (io/file path))))

(defn compare-entry [engine-root entry]
  (let [authority (contracts/load-contract entry)
        path (engine-path engine-root entry)
        file (io/file path)]
    (cond
      (and (not (.exists file))
           (retired-engine-fixture? engine-root entry))
      {:entry entry :path path :ok? true :status :retired-engine-fixture}

      (not (.exists file))
      {:entry entry :path path :ok? false :problem :missing-engine-file}

      :else
      (let [legacy (read-file-edn path)]
        (if (= authority legacy)
          {:entry entry :path path :ok? true}
          {:entry entry :path path :ok? false :problem :edn-mismatch})))))

(defn compare-all
  ([] (compare-all default-engine-root))
  ([engine-root]
   (let [entries (:contracts (contracts/catalog))
         results (mapv #(compare-entry engine-root %) entries)
         failures (vec (remove :ok? results))]
     {:ok? (empty? failures)
      :engine-root engine-root
      :checked (count results)
      :results results
      :failures failures})))

(defn usage []
  (str "Usage:\n"
       "  clojure -M:check-engine-mirror [../kami-engine]\n"))

(defn -main [& args]
  (let [engine-root (or (first args) default-engine-root)
        result (compare-all engine-root)]
    (if (:ok? result)
      (println "scene-mirror-ok" (:checked result) "files")
      (do
        (binding [*out* *err*]
          (println "scene mirror mismatch against" engine-root)
          (doseq [{:keys [entry path problem]} (:failures result)]
            (println (name problem) (pr-str entry) path)))
        (System/exit 1)))))
