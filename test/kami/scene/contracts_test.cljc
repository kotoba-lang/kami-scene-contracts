(ns kami.scene.contracts-test
  (:require [clojure.string :as str]
            [clojure.test :refer [deftest is testing]]
            [kami.scene.contracts :as contracts]
            [kami.scene.fixture-sync :as fixtures]
            [kami.scene.mirror-check :as mirror]))

(deftest catalog-loads
  (let [cat (contracts/catalog)
        result (contracts/validate-catalog cat)]
    (is (:ok? result))
    (is (= 29 (:entry-count result)))
    (is (some #(= {:domain :kami-terrain-scene :file "biomes.edn"} %)
              (:contracts cat)))
    (is (some #(= {:domain :kami-mangaka-scene :file "pose_lexicon.edn"} %)
              (:contracts cat)))))

(deftest all-contract-edn-loads
  (let [result (contracts/validate-all)
        loaded (contracts/load-contracts)]
    (testing "all catalog entries are readable EDN contract data"
      (is (:ok? result))
      (is (= 29 (count loaded))))
    (testing "selected domains are present"
      (is (contains? loaded {:domain :kami-character-scene :file "hair.edn"}))
      (is (contains? loaded {:domain :kami-vehicle-scene :file "ground.edn"}))
      (is (contains? loaded {:domain :kami-live :file "audio.edn"}))
      (is (contains? loaded {:domain :kami-mangaka-scene :file "camera.edn"}))
      (is (contains? loaded {:domain :kami-mangaka-scene :file "expression_lexicon.edn"}))
      (is (contains? loaded {:domain :kami-mangaka-scene :file "pose_lexicon.edn"}))
      (is (contains? loaded {:domain :kami-mangaka-scene :file "render.edn"})))))

(deftest catalog-covers-authority-source-files
  (let [coverage (:source-coverage (contracts/validate-all))
        drift (contracts/validate-source-coverage
               (update (contracts/catalog) :contracts
                       (partial remove #(= {:domain :kami-live :file "audio.edn"} %))))]
    (is (:ok? coverage))
    (is (= 29 (:source-count coverage)))
    (is (= ["kami/scene/contracts/kami-live/audio.edn"]
           (:missing-from-catalog drift)))))

(deftest legacy-engine-mirror-check
  (let [result (mirror/compare-all "../kami-engine")]
    (is (:ok? result))
    (is (= 29 (:checked result)))
    (is (every? #(or (:ok? %)
                     (:problem %))
                (:results result)))))

(deftest engine-fixture-sync-plan
  (let [plan (fixtures/sync-plan "../kami-engine")
        check (fixtures/check-plan plan)]
    (is (= 29 (count plan)))
    (is (:ok? check))
    (is (every? #{:same :retired} (map :status plan)))
    (is (some #(= {:domain :kami-terrain-scene :file "biomes.edn"} (:entry %))
              plan))))

(deftest authority-manifest-covers-engine-fixtures
  (let [{:keys [ok? contract-count manifest]} (contracts/validate-authority-manifest)
        entries (:contracts manifest)
        terrain (some #(when (= {:domain :kami-terrain-scene :file "biomes.edn"}
                         (select-keys % [:domain :file]))
                         %)
                      entries)]
    (is ok?)
    (is (= 29 contract-count))
    (is (= :engine-data-mirrors-authority (:fixture-policy manifest)))
    (is (= "kami/scene/contracts/kami-terrain-scene/biomes.edn"
           (:authority-resource terrain)))
    (is (= "orgs/kotoba-lang/kami-engine/kami-terrain-scene/data/biomes.edn"
           (:engine-fixture-path terrain)))
    (is (= 64 (count (:sha256 terrain))))
    (is (pos-int? (:byte-count terrain)))))

(deftest mangaka-scene-pose-lexicon-covers-core-labels
  (let [contract (contracts/load-contract
                  {:domain :kami-mangaka-scene :file "pose_lexicon.edn"})
        {:keys [aliases presets]} (:mangaka/pose-presets contract)
        resolve-label (fn [label] (get presets (get aliases label label)))]
    (testing "every label from tests/p1_smoke.rs::pose_lexicon_covers_core_labels resolves"
      (doseq [label ["action.rest" "action.idle" "action.dash" "action.run"
                     "action.walk" "action.swing" "action.attack" "action.hit"
                     "action.impact" "action.fall" "action.cower" "action.flinch"
                     "action.shout" "action.yell" "action.point" "action.reach"
                     "action.stand_proud" "action.heroic"]]
        (is (some? (resolve-label label)) (str "missing preset: " label))))
    (testing "unknown labels do not resolve"
      (is (nil? (resolve-label "action.flarble")))
      (is (nil? (resolve-label ""))))
    (testing "action.dash has arm and leg rotations (parity w/ p1_smoke.rs)"
      (let [bones (set (map :bone (resolve-label "action.dash")))]
        (is (contains? bones "leftUpperArm"))
        (is (contains? bones "rightUpperArm"))
        (is (contains? bones "leftUpperLeg"))
        (is (contains? bones "rightUpperLeg"))))
    (testing "rest is an alias for action.rest, an empty preset"
      (is (= [] (resolve-label "rest") (resolve-label "action.rest"))))))

(deftest mangaka-scene-expression-lexicon-canonicalises-aliases
  (let [contract (contracts/load-contract
                  {:domain :kami-mangaka-scene :file "expression_lexicon.edn"})
        {:keys [aliases default canonical]} (:mangaka/expression-presets contract)
        resolve-name (fn [name]
                       (get aliases (str/lower-case name) default))]
    (is (= 8 (count canonical)))
    (is (= :happy (resolve-name "happy")))
    (is (= :happy (resolve-name "JOY")))
    (is (= :angry (resolve-name "rage")))
    (is (= :determined (resolve-name "focus")))
    (is (= :neutral (resolve-name "???")))))

(deftest mangaka-scene-camera-and-render-defaults
  (let [camera (contracts/load-contract {:domain :kami-mangaka-scene :file "camera.edn"})
        render (contracts/load-contract {:domain :kami-mangaka-scene :file "render.edn"})]
    (is (= :medium-shot (get-in camera [:mangaka/camera-defaults :shot])))
    (is (= 3 (count (:mangaka/light-presets camera))))
    (is (= 1024 (get-in render [:mangaka/render-defaults :width])))
    (is (= 1448 (get-in render [:mangaka/render-defaults :height])))
    (is (= 0x000f (get-in render [:mangaka/render-passes :all])))
    (is (= 6 (count (:mangaka/fx-kinds render))))
    (is (= [:environment :camera :lights]
           (get-in render [:mangaka/scene-jsonld-schema :roundtrips])))))
